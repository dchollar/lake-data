plugins {
    id 'base'
    id 'war'
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
}

java {
    sourceCompatibility(JavaVersion.VERSION_17)
    targetCompatibility(JavaVersion.VERSION_17)
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

springBoot {
    buildInfo()
}

configurations.configureEach {
    exclude group: 'commons-logging', module: 'commons-logging'
}

ext {
    OPENAPI_VERSION = '2.2.0'
    POI_VERSION = '5.2.4'
    SPOCK_VERSION = '2.4-M1-groovy-4.0'
}

dependencies {
    implementation('org.apache.groovy:groovy-all:4.0.15')

    implementation('org.apache.commons:commons-lang3:3.13.0')
    implementation('commons-io:commons-io:2.15.0')
    implementation('org.apache.pdfbox:pdfbox:3.0.0')
    implementation('net.sourceforge.tess4j:tess4j:5.8.0')
    implementation("org.apache.poi:poi:${POI_VERSION}")
    implementation("org.apache.poi:poi-ooxml:${POI_VERSION}")

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-cache')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.thymeleaf.extras:thymeleaf-extras-springsecurity6')

    implementation('org.hibernate.orm:hibernate-spatial:6.3.1.Final')
    implementation('org.hibernate.validator:hibernate-validator:8.0.1.Final')

    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:${OPENAPI_VERSION}")
    implementation("org.springdoc:springdoc-openapi-starter-common:${OPENAPI_VERSION}")
    implementation('com.mysql:mysql-connector-j:8.2.0')
    implementation('org.yaml:snakeyaml:2.2')

    annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation("org.spockframework:spock-core:${SPOCK_VERSION}")
    testImplementation("org.spockframework:spock-spring:${SPOCK_VERSION}")
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

def generateMavenPom = tasks.register("generateMavenPom", GenerateMavenPom) {
    destination = file("pom.xml")
    pom = publishing.publications.maven.pom
}

tasks.compileJava.dependsOn generateMavenPom